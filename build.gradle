plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.github.yannicklamprecht.spigot.tools" version "1.0.0"
}

group = 'net.slqmy'
version = '0.1'

repositories {
    mavenCentral()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
        }
    }

    maven {
        url "https://repo.purpurmc.org/snapshots"
    }

    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        url = 'https://oss.sonatype.org/content/repositories/central'
    }

    mavenLocal()
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.20.1-R0.1-SNAPSHOT:remapped-mojang'
    compileOnly "org.purpurmc.purpur:purpur-api:1.20.1-R0.1-SNAPSHOT"
    implementation 'org.mongodb:mongodb-driver-sync:4.10.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    exclude 'META-INF/*.DSA', 'META-INF/*.SF', 'META-INF/*.RSA'
    relocate 'org.mongodb', 'net.slqmy.tss_core.shaded.org.mongodb'
    mergeServiceFiles()
}

tasks.register('obfuscate', Exec) {
    description 'Generates an obfuscated version of the jar for use with Spigot.'
    group = 'jar preparation'
    workingDir "${buildDir}"
    ext.toolingDir = System.getProperty("user.dir")
    ext.homeDir = project.gradle.gradleUserHomeDir.parent
    commandLine 'java', '-jar', ext.toolingDir + '/tooling/specialsource/SpecialSource.jar', '-i', 'libs/' + rootProject.name + '.jar',
            '-o', 'libs/' + rootProject.name + '-obf.jar', '-m',
            ext.homeDir + '/.m2/repository/org/spigotmc/minecraft-server/1.20.1-R0.1-SNAPSHOT/minecraft-server-1.20.1-R0.1-SNAPSHOT-maps-mojang.txt',
            '--reverse'
}

tasks.register('deobfuscate', Exec) {
    description 'Generates a deobfuscated version of the jar for development purposes. Resulting jar cannot be used with Spigot.'
    group = 'jar preparation'
    workingDir "${buildDir}"
    ext.toolingDir = System.getProperty("user.dir")
    ext.homeDir = project.gradle.gradleUserHomeDir.parent
    commandLine 'java', '-jar', ext.toolingDir + '/tooling/specialsource/SpecialSource.jar', '-i', 'libs/' + rootProject.name + '-obf.jar',
            '-o', 'libs/' + rootProject.name + '.jar', '-m',
            ext.homeDir + '/.m2/repository/org/spigotmc/minecraft-server/1.20.1-R0.1-SNAPSHOT/minecraft-server-1.20.1-R0.1-SNAPSHOT-maps-spigot.csrg'
}

tasks {
    spigotTools {
        mojangMapped.set(true)
        version.set("1.20.1")
        spigotVersion.set("1.20.1-R0.1-SNAPSHOT")
        outputClassifier.set("spigot-mapped")
    }
}
